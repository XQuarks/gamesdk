plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        versionCode 9
        // LINT.IfChange
        versionName '0.24'
        // LINT.ThenChange(../README.md)
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

dependencies {
    implementation project(path: ':memoryadvice')
    implementation project(path: ':memoryadvice_common')
    implementation 'commons-io:commons-io:2.6'
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    exclude "**/AndroidManifest.xml"
    failOnError false
}
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.google.android.games'
                artifactId = 'memory-test'
                version = android.defaultConfig.versionName
                artifact(sourceJar)
                pom {
                    name = 'Memory Advice tests'
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}
