import org.gradle.internal.os.OperatingSystem;

apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    ndkVersion "20.0.5594570"
    defaultConfig {
        applicationId "com.tuningfork.experimentsdemo"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 17
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    buildFeatures {
        prefab true
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    // Uncomment this line and comment out "implementation project("...")"
    // to use a locally built .aar
    //implementation fileTree(dir: '../../../', include: ['games-performance-tune-release.aar',
    //                                                    'games-frame-pacing-release.aar'])
    implementation project(':games-performance-tuner')
    implementation project(':games-frame-pacing')

    // GMS Core dependencies
    implementation "com.google.android.gms:play-services-clearcut:16.0.0"

    testImplementation 'junit:junit:4.12'
}

task createJar(type: GradleBuild) {
    buildFile = '../../../../games-performance-tuner/tools/validation/build.gradle'
    tasks = ['createJar']
}

def getProtocPath() {
    String platformName
    if (OperatingSystem.current().isLinux()) platformName = "linux-x86/bin/protoc"
    if (OperatingSystem.current().isMacOsX()) platformName = "mac/bin/protoc"
    if (OperatingSystem.current().isWindows()) platformName = "win/bin/protoc"
    return "../../../../third_party/protobuf-3.0.0/install/"  + platformName
}

task buildTuningForkBinFiles(type: JavaExec) {
    dependsOn createJar
    main "-jar"
    args(   "../../../../games-performance-tuner/tools/validation/build/libs/TuningforkApkValidationTool.jar",
            "--tuningforkPath",
            "src/main/assets/tuningfork",
            "--protoCompiler",
            getProtocPath())
}

tasks.preBuild.dependsOn("buildTuningForkBinFiles")
