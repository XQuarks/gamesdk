apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

def gamesdkDir = "../../../../../../../"


android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId 'com.google.gamesdk.gamecert.operationrunner'
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            // Ultimately requires ndkVersion to be explicit, or build fails with
            // > No version of NDK matched the requested version 20.0.5594570.
            ndkVersion "20.1.5948944"
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                version "3.10.2"
                // make gamesdk location available to our cmake build
                arguments = [
                        "-DGAMESDK_DIR=${gamesdkDir}".toString()
                ]
            }
        }

        aaptOptions {
            // This avoids the Android Asset Packager Tool (aapt) to compress files of any of this
            // already compressed extensions.
            noCompress "astc", "pkm", "basis", "gzip", "lz4"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 
                'proguard-rules.pro'
        }

        optimizedNative {
            initWith debug

            externalNativeBuild {
                cmake {
                    // compile native-lib optimized
                    arguments << '-DCMAKE_BUILD_TYPE:STRING=Release'
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    externalNativeBuild {
        cmake {
            version "3.10.2"
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
}

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.18'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    // add gamesdk deps
    implementation(name:"aar-release", ext: "aar")

    // Use protobuf to read the results from Android Game SDK
    implementation 'com.google.protobuf:protobuf-java:3.8.0'
    protobuf files('../../../../../include/device_info/device_info.proto')
}
